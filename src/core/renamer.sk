namespace GLSLX.Renamer {
  def rename(globals List<Node>, options CompilerOptions) StringMap<string> {
    return Renamer.new(options.renameSymbols, options.ignoredSymbols)._rename(globals)
  }

  class SymbolInfo {
    const name string
    const label int
    const symbols IntMap<Symbol> = {} # One from each duplicate shader compilation
    var useCount = 0
    var isArgumentOrLocalVariable = false
  }

  const _first = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_"
  const _rest = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_0123456789"

  def _numberToName(number int) string {
    var name = _first.get(number % _first.count)
    number = number / _first.count
    while number > 0 {
      number--
      name += _rest.get(number % _rest.count)
      number = number / _rest.count
    }
    return name
  }
}

class GLSLX.Renamer {
  var _renameSymbols RenameSymbols
  var _ignoredSymbols List<string>
  var _functionList List<SymbolInfo> = []
  var _symbolInfoList List<SymbolInfo> = []
  var _symbolInfoMap IntMap<SymbolInfo> = {}
  var _localVariableUnionFind = UnionFind.new
  var _namingGroupsUnionFind = UnionFind.new
  var _globalIndex = 0
  var _nextSymbolName = 0
  var _enclosingFunctionLabel = -1

  def _rename(globals List<Node>) StringMap<string> {
    # Gather information
    for i in 0..globals.count {
      _globalIndex = i
      _scanForSymbols(globals[i])
    }

    # Compact names of unrelated things
    _aliasArgumentAndLocalVariablesFromDifferentFunctions

    # Do the renaming
    var renaming StringMap<string> = {}
    var groups = _extractGroups(_namingGroupsUnionFind, null)
    groups.sort((a, b) => _countUses(b) <=> _countUses(a))
    for group in groups {
      var name string = null
      for info in group {
        for symbol in info.symbols.values {
          if symbol.name in _ignoredSymbols {
            continue
          }
          var old = symbol.name
          if !symbol.isImportedOrExported && (_renameSymbols == .ALL || _renameSymbols == .INTERNAL_ONLY && !symbol.isAttributeOrUniform) {
            if name == null {
              name = _generateSymbolName
            }
            symbol.name = name
          }
          if !symbol.isImported && symbol.isAttributeOrUniform {
            renaming[old] = symbol.name
          }
        }
      }
    }
    return renaming
  }

  def _scanForSymbols(node Node) {
    if node.symbol != null {
      _recordSymbol(node.symbol).useCount++
    }

    for child = node.firstChild; child != null; child = child.nextSibling {
      _scanForSymbols(child)
    }

    switch node.kind {
      case .VARIABLE {
        var variable = node.symbol.asVariable
        _scanForSymbols(variable.type)
        if variable.value != null {
          _scanForSymbols(variable.value)
        }
      }

      case .FUNCTION {
        assert(node.symbol.id in _symbolInfoMap) # Should be added from _recordSymbol() above
        var function = node.symbol.asFunction
        _enclosingFunctionLabel = _symbolInfoMap[node.symbol.id].label
        if function.sibling != null {
          _namingGroupsUnionFind.union(_enclosingFunctionLabel, _recordSymbol(function.sibling).label)
        }
        _scanForSymbols(function.returnType)
        for argument in function.arguments {
          _recordSymbol(argument).useCount++
          _scanForSymbols(argument.type)
        }
        if function.block != null {
          _scanForSymbols(function.block)
        }
        _enclosingFunctionLabel = -1
      }
    }
  }

  def _recordSymbol(symbol Symbol) SymbolInfo {
    var info = _symbolInfoMap.get(symbol.id, null)

    if info == null {
      info = SymbolInfo.new(symbol.name, _symbolInfoList.count)
      info.isArgumentOrLocalVariable = symbol.isArgumentOrLocalVariable
      _symbolInfoList.append(info)
      _symbolInfoMap[symbol.id] = info
      _localVariableUnionFind.allocate
      _namingGroupsUnionFind.allocate
    }

    if !(_globalIndex in info.symbols) {
      info.symbols[_globalIndex] = symbol
    }

    if symbol.isArgumentOrLocalVariable {
      _localVariableUnionFind.union(_enclosingFunctionLabel, info.label)
    }

    return info
  }

  def _generateSymbolName string {
    while true {
      var name = _numberToName(_nextSymbolName)
      _nextSymbolName++
      if name in Tokenizer.keywords || name in Tokenizer.reservedWords || name.startsWith("gl_") || name in _ignoredSymbols {
        continue
      }
      return name
    }
  }

  def _extractGroups(unionFind UnionFind, filter fn(SymbolInfo) bool) List<List<SymbolInfo>> {
    var labelToGroup IntMap<List<SymbolInfo>> = {}
    for info in _symbolInfoList {
      if filter != null && !filter(info) {
        continue
      }
      var label = unionFind.find(info.label)
      var group = labelToGroup.get(label, null)
      if group == null {
        group = []
        labelToGroup[label] = group
      }
      group.append(info)
    }
    return labelToGroup.values
  }

  def _aliasArgumentAndLocalVariablesFromDifferentFunctions {
    _zipTogetherInOrder(_extractGroups(_localVariableUnionFind, info => info.isArgumentOrLocalVariable))
  }

  def _zipTogetherInOrder(groups List<List<SymbolInfo>>) {
    var labels List<int> = []
    for group in groups {
      group.sort((a, b) => b.useCount <=> a.useCount)
      for i in 0..group.count {
        var info = group[i]
        if i < labels.count {
          _namingGroupsUnionFind.union(info.label, labels[i])
        } else {
          labels.append(info.label)
        }
      }
    }
  }

  def _countUses(group List<SymbolInfo>) int {
    var total = 0
    for info in group {
      total += info.useCount
    }
    return total
  }
}
