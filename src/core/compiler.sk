namespace GLSLX {
  enum OutputFormat {
    JSON
    CPP
    SKEW
  }

  enum RenameSymbols {
    ALL
    INTERNAL_ONLY
    NONE
  }

  class CompilerOptions {
    var compactSyntaxTree = true
    var removeWhitespace = true
    var renameSymbols = RenameSymbols.ALL
    var trimSymbols = true
  }

  enum ExtensionBehavior {
    DEFAULT
    DISABLE
    ENABLE
    REQUIRE
    WARN
  }

  class CompilerData {
    const currentExtensions StringMap<ExtensionBehavior> = {}
    var _nextSymbolID = 0

    def nextSymbolID int {
      _nextSymbolID++
      return _nextSymbolID
    }

    def extensionBehavior(name string) ExtensionBehavior {
      return currentExtensions.get(name, .DEFAULT)
    }
  }

  class CompilerResult {
    const shaders List<Source>
    const renaming StringMap<string>

    def output(format OutputFormat) string {
      switch format {
        case .JSON {
          return dynamic.JSON.stringify({
            "shaders": shaders == null ? null : shaders.map<dynamic>(source => ({
              "name": source.name,
              "contents": source.contents,
            })),
            "renaming": renaming,
          }, null, 2) + "\n"
        }

        case .CPP {
          if shaders != null {
          var code = ""
            for shader in shaders {
              code += "static const char GLSLX_SOURCE_" + _transformName(shader.name) + "[] = " + dynamic.JSON.stringify(shader.contents) + ";\n"
            }
            if renaming != null {
              code += "\n"
              for name in renaming.keys {
                code += "static const char GLSLX_NAME_" + _transformName(name) + "[] = " + dynamic.JSON.stringify(renaming[name]) + ";\n"
              }
            }
            return code
          }
        }

        case .SKEW {
          if shaders != null {
          var code = ""
            for shader in shaders {
              code += "const GLSLX_SOURCE_" + _transformName(shader.name) + " = " + dynamic.JSON.stringify(shader.contents) + "\n"
            }
            if renaming != null {
              code += "\n"
              for name in renaming.keys {
                code += "const GLSLX_NAME_" + _transformName(name) + " = " + dynamic.JSON.stringify(renaming[name]) + "\n"
              }
            }
            return code
          }
        }
      }
      return null
    }

    def _transformName(name string) string {
      return (name as dynamic).replace(dynamic.RegExp.new("([a-z0-9])([A-Z])", "g"), "$1_$2").toUpperCase()
    }
  }
}

namespace GLSLX.Compiler {
  def compile(log Log, sources List<Source>, options CompilerOptions) CompilerResult {
    if log.hasErrors {
      return null
    }

    # Generate tokens once
    sources.prepend(Source.new("<api>", API))
    for source in sources {
      source.tokens = Tokenizer.tokenize(log, source)
    }
    if log.hasErrors {
      return null
    }

    var global = Node.createGlobal
    var scope = Scope.new(.GLOBAL, null)
    var data = CompilerData.new

    # Parse everything next
    for source in sources {
      Parser.parse(log, source.tokens, global, data, scope)
    }
    if log.hasErrors {
      return null
    }

    # Then run type checking
    Resolver.resolve(log, global, data)
    if log.hasErrors {
      return null
    }

    # Multiple export mode is more complicated. Everything is already compiled,
    # and in theory we could quickly export all shaders from that, but in
    # practice it's simpler if the source code is just compiled over again once
    # per shader.
    var names List<string> = []
    var globals List<Node> = []
    for root in _collectAllExportedFunctions(scope) {
      var shaderGlobal = Node.createGlobal
      var shaderScope = Scope.new(.GLOBAL, null)
      var shaderData = CompilerData.new
      var shaderLog = Log.new

      # Parse everything again
      for source in sources {
        Parser.parse(shaderLog, source.tokens, shaderGlobal, shaderData, shaderScope)
      }

      # Flow types through the tree
      Resolver.resolve(shaderLog, shaderGlobal, shaderData)

      # Optimize it and trim it down
      _unexportAllFunctionsExcept(shaderScope, root)
      Rewriter.rewrite(shaderGlobal, options)
      globals.append(shaderGlobal)
      names.append(root.name)
    }

    # Rename everything together
    var shaders List<Source> = []
    var renaming = Renamer.rename(globals, options)
    for i in 0..names.count {
      shaders.append(Source.new(names[i], Emitter.emit(globals[i], options)))
    }
    return CompilerResult.new(shaders, renaming)
  }

  def _collectAllExportedFunctions(scope Scope) List<FunctionSymbol> {
    var symbols List<FunctionSymbol> = []
    for symbol in scope.symbols.values {
      if symbol.isFunction && symbol.isExported {
        symbols.append(symbol.asFunction)
      }
    }
    return symbols
  }

  def _unexportAllFunctionsExcept(scope Scope, function FunctionSymbol) {
    for symbol in scope.symbols.values {
      if symbol.id != function.id {
        symbol.flags &= ~.EXPORTED
      } else {
        symbol.name = "main"
        var sibling = symbol.asFunction.sibling
        if sibling != null {
          sibling.name = symbol.name
        }
      }
    }
  }
}
